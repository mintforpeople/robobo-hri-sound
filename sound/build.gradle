apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 34
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 33
        versionName "1.2.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
    sourceSets {
        main {
            assets.srcDirs += "$buildDir/generated/assets"
        }
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.13.2'
    api 'com.mytechia:robobo-framework:1.3.4'
    api 'com.mytechia:robobo-remote-control:1.0.2'
    api files('libs/TarsosDSP-Android-latest.jar')
    implementation group: 'com.alphacephei', name: 'vosk-android', version: '0.3.47'
}


/*
New local publish module
TODO: Make it able to release on OSS Sonatype to MavenCentral. Not needed by now, we can just upload to the local repo.

Using maven-publish and standard methods to push to maven.
To do the same on other modules, just replace the Chris Bane's script with this
Use gradle publishing:publishLibraryPublicationToLocalNexusRepository to upload artifacts to the repo

If the version ends in -SNAPSHOT, the snapshot repo will be selected.
All of the release info is stored in gradle.properties

Nexus auth should be done in the user's home gradle.properties file (uncommited for security reasons)
 */

afterEvaluate {
    publishing {
        repositories {
            maven {
                name 'LocalNexus'
                allowInsecureProtocol = true
                credentials {
                    username NEXUS_USER
                    password NEXUS_PASSWORD
                }
                if(VERSION_NAME.endsWith('-SNAPSHOT')) {
                    url SNAPSHOT_REPOSITORY_URL
                } else {
                    url RELEASE_REPOSITORY_URL
                }
            }
        }

        publications {
            library(MavenPublication) {
                from components.release
                groupId GROUP
                artifactId POM_ARTIFACT_ID
                version VERSION_NAME
            }
        }
    }
}